buildscript {
    ext {
        versions = [
                kotlin: '1.3.72',
                koin: '2.1.6'
        ]
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.koin:koin-gradle-plugin:$versions.koin"
    }
}

apply from: 'deps.gradle'

subprojects {
    repositories {
        google()
        jcenter()
    }

    // base java compile config
    tasks.withType(JavaCompile).all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    // base kotlin compile config
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            freeCompilerArgs = [ "-Xjsr305=strict" ]
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }

    // base config for java library modules
    plugins.withType(JavaLibraryPlugin).whenPluginAdded {
        dependencies {
            implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
            implementation libraries.kotlin
        }
    }

    // base config for android modules
    plugins.withType(com.android.build.gradle.BasePlugin).whenPluginAdded {
        android {
            compileSdkVersion 29
            buildToolsVersion "29.0.2"

            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 29

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }

            buildTypes {
                release {
                    minifyEnabled true
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            sourceSets {
                main.java.srcDirs += 'src/main/kotlin'
            }
        }

        dependencies {
            implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
            implementation libraries.kotlin
        }
    }

    // base config for android application modules
    plugins.withType(com.android.build.gradle.AppPlugin).whenPluginAdded {
        android {
            buildTypes {
                debug {
                    applicationIdSuffix ".debug"
                }

                release {
                    shrinkResources true
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
