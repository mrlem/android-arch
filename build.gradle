// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin_version = '1.3.71'
        koin_version = '2.1.5'
        libraries = [
                di: [
                        "org.koin:koin-android:$koin_version",
                        "org.koin:koin-android-scope:$koin_version",
                        "org.koin:koin-android-viewmodel:$koin_version"
                ],
                rx: [
                        "io.reactivex.rxjava3:rxjava:3.0.2",
                        "io.reactivex.rxjava3:rxandroid:3.0.0",
                        "io.reactivex.rxjava3:rxkotlin:3.0.0"
                ]
        ]
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath "org.koin:koin-gradle-plugin:$koin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    // make all kotlin compiles java 1.8 compliant
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }

    // base config for java library modules
    plugins.withType(JavaLibraryPlugin).whenPluginAdded {
        dependencies {
            implementation fileTree(dir: 'libs', include: ['*.jar'])
            implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        }
    }

    // base config for android modules
    plugins.withType(com.android.build.gradle.BasePlugin).whenPluginAdded {
        android {
            compileSdkVersion 29
            buildToolsVersion "29.0.2"

            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 29

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }
        }
        dependencies {
            implementation fileTree(dir: 'libs', include: ['*.jar'])
            implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
