buildscript {
    ext {
        versions = [
                kotlin: '1.3.71',
                koin: '2.1.5'
        ]
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.koin:koin-gradle-plugin:$versions.koin"
    }
}

apply from: 'deps.gradle'

subprojects {
    repositories {
        google()
        jcenter()
    }

    // base kotlin compile config: java 1.8 compliant
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }

    // base config for java library modules
    plugins.withType(JavaLibraryPlugin).whenPluginAdded {
        dependencies {
            implementation libraries.kotlin
        }
    }

    // base config for android modules
    plugins.withType(com.android.build.gradle.BasePlugin).whenPluginAdded {
        android {
            compileSdkVersion 29
            buildToolsVersion "29.0.2"

            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 29

                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }
        }
        dependencies {
            implementation libraries.kotlin
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
